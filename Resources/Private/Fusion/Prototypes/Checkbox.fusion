prototype(Neos.Fusion.Form:Checkbox)  < prototype(Neos.Fusion.Form:FieldComponent) {

    renderer = Neos.Fusion:Component {

        #
        # pass down props
        #
        attributes = ${props.attributes}
        content = ${props.content}

        #
        # the `checked` state is calculated outside the renderer to allow` overriding via `attributes`
        #
        checked = false
        checked.@process.checkMultiValue = ${Array.indexOf(field.getCurrentValue(true), field.getTargetValue(true)) > -1}
        checked.@process.checkMultiValue.@if.hasValue = ${field.hasCurrentValue()}
        checked.@process.checkMultiValue.@if.isMultiple = ${field.isMultiple()}
        checked.@process.checkSingleValue = ${field.getCurrentValue(true) == field.getTargetValue(true)}
        checked.@process.checkSingleValue.@if.hasValue = ${field.hasCurrentValue()}
        checked.@process.checkSingleValue.@if.isSingle = ${!field.isMultiple()}

        renderer = afx`
            <input
                type="checkbox"
                name={field.getName()}
                value={field.getTargetValue(true)}
                checked={props.checked}
                {...props.attributes}
            />
        `
    }

}

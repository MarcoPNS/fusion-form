prototype(Neos.Fusion.Form:Checkbox)  < prototype(Neos.Fusion.Form:FieldComponent) {
    @propTypes {
        checked = ${PropTypes.boolean}
    }

    checked = false

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        @apply.override = Neos.Fusion:DataStructure {
            checked = Neos.Fusion:Case {
                hasSingleValue {
                    condition = ${field.value && !field.multiple}
                    renderer = ${field.value == props.value}
                }
                hasMultiValue {
                    condition = ${field.value && field.multiple}
                    renderer = ${Array.indexOf(field.value, props.value) > -1}
                }
                default {
                    condition = true
                    renderer = ${props.checked}
                }
            }
        }

        renderer = afx`
            <input
                {...props.attributes}
                id={props.id}
                class={props.class}
                required={props.required}
                type="checkbox"
                name={field.name}
                value={Form.stringifyValue(props.value)}
                checked={props.checked}
            />
        `
    }
}

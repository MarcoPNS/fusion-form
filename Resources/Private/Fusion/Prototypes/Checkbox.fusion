prototype(Neos.Fusion.Form:Checkbox)  < prototype(Neos.Fusion.Form:FieldComponent) {
    @propTypes {
        multiple = ${PropTypes.boolean}
        checked = ${PropTypes.boolean}
    }

    multiple = false
    checked = false

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        @apply.override = Neos.Fusion:DataStructure {
            checked = Neos.Fusion:Case {
                hasSingleValue {
                    condition = ${field.value && !props.multiple}
                    renderer = ${field.value == props.value}
                }
                hasMultiValue {
                    condition = ${field.value && props.multiple}
                    renderer = ${Array.indexOf(field.value, props.value) > -1}
                }
                default {
                    condition = true
                    renderer = ${props.checked}
                }
            }
        }

        renderer = afx`
            <input
                {...props.attributes}
                id={props.id}
                class={props.class}
                required={props.required}
                type="checkbox"
                name={field.name + (props.multiple ? '[]' : '')}
                value={Form.stringifyValue(props.value)}
                checked={props.checked}
            />
        `
    }
}

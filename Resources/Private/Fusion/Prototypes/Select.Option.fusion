prototype(Neos.Fusion.Form:Select.Option)  < prototype(Neos.Fusion:Component) {
    @propTypes {
        content = ${PropTypes.string}
        value = ${PropTypes.any}
        selected = ${PropTypes.boolean}
    }

    selected = false
    value = null
    content = null

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        @apply.override = Neos.Fusion:DataStructure {
            selected = Neos.Fusion:Case {
                hasSingleValue {
                    condition = ${field.value && !field.multiple}
                    renderer = ${Form.stringifyValue(props.value) == field.value}
                }
                hasMultiValue {
                    condition = ${field.value && field.multiple}
                    renderer = ${Array.indexOf(field.value, Form.stringifyValue(props.value)) > -1}
                }
                default {
                    condition = true
                    renderer = ${props.selected}
                }
            }
        }

        renderer = afx`
           <option value={Form.stringifyValue(props.value)} selected={props.selected}>{props.content || props.value}</option>
        `
    }
}

prototype(Neos.Fusion.Form:Select.Option)  < prototype(Neos.Fusion:Component) {

    #
    # ensure the `option`-context is evaluated first so it is available for calculating other `props`
    #
    @context.option = ${this.option}

    #
    # `option` is excluded from `props`, for beeing in line with `form` and `field`
    #
    @ignoreProperties = ${['option']}

    @propTypes {
        option = ${PropTypes.dataStructure({value: propTypes.any})}
        attributes = ${PropTypes.dataStructure()}
        content = ${PropTypes.string}
    }

    #
    # the values are used to calculate the `option` value and selected state
    #
    option = Neos.Fusion:DataStructure {
        value = null
    }

    #
    # attributes for the field to be used by the renderer of derived types
    #
    attributes = Neos.Fusion:DataStructure

    #
    # afx content to be used by the renderer of derived types
    #
    content = ''

    renderer = Neos.Fusion:Component {

        #
        # pass down props
        #
        attributes = ${props.atttributes}
        content = ${props.content}

        #
        # the `selected` state is calculated outside the renderer to allow` overriding via `attributes`
        #
        selected = false
        selected.@process.checkMultiValue = ${Array.indexOf(Form.stringifyArray(field.getValue()), Form.stringifyValue(option.value)) > -1}
        selected.@process.checkMultiValue.@if.hasValue = ${field.getValue()}
        selected.@process.checkMultiValue.@if.isMultiple = ${field.isMultiple()}
        selected.@process.checkSingleValue = ${Form.stringifyValue(field.getValue()) == Form.stringifyValue(option.value)}
        selected.@process.checkSingleValue.@if.hasValue = ${field.getValue()}
        selected.@process.checkSingleValue.@if.isSingle = ${!field.isMultiple()}

        renderer = afx`
           <option
               value={Form.stringifyValue(option.value)}
               selected={props.selected}
               {...props.attributes}
           >
               {props.content || option.value}
           </option>
        `
    }
}

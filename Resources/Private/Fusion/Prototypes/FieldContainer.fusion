prototype(Neos.Fusion.Form:FieldContainer)  < prototype(Neos.Fusion:Component) {
    @context.field = ${Form.createFieldDefinition(this.form, this.name, this.property)}

    @propTypes {
        form = ${PropTypes.instanceOf('Neos\Fusion\Form\Domain\Model\FormDefinition')}
        name = ${PropTypes.string}
        property = ${PropTypes.string}
        id = ${PropTypes.string}
        class = ${PropTypes.string}
        errorClass = ${PropTypes.string}
        label = ${PropTypes.string}
        content = ${PropTypes.string}
    }

    form = ${form}
    name = null
    property = null

    id = null
    class = 'fieldContainer'
    errorClass = 'error'
    label = null
    content = null

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        @apply.override = Neos.Fusion:DataStructure {
            class = Neos.Fusion:Join {
                @glue = ' '
                class = ${props.class}
                errorClass = ${props.errorClass}
                errorClass.@if.hasErrors = ${field.validationResult.flattenedErrors}
            }
        }

        renderer = afx`
            <div id={props.is} class={props.class}>
                <label @if.has={props.label}>{props.label}</label>
                {props.content}
                <ul @if.hasErrors={field.validationResult.flattenedErrors} class={props.errorClass}>
                    <Neos.Fusion:Loop items={field.validationResult.flattenedErrors} itemName="errors" >
                        <Neos.Fusion:Loop items={errors} itemName="error" >
                            <li>{error}</li>
                        </Neos.Fusion:Loop>
                    </Neos.Fusion:Loop>
                </ul>
            </div>
        `
    }
}


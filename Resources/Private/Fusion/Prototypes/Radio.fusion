prototype(Neos.Fusion.Form:Radio)  < prototype(Neos.Fusion.Form:FieldComponent) {
    @propTypes {
        checked = ${PropTypes.boolean}
    }

    checked = false

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        @apply.override = Neos.Fusion:DataStructure {
            checked = Neos.Fusion:Case {
                hasValue {
                    condition = ${field.value}
                    renderer = ${field.value == props.value}
                }
                default {
                    condition = true
                    renderer = ${props.checked}
                }
            }
        }

        renderer = afx`
            <input
                {...props.attributes}
                id={props.id}
                class={props.class}
                type="radio"
                name={field.name}
                value={Form.stringifyValue(props.value)}
                required={props.required}
                checked={props.checked}
            />
        `
    }
}

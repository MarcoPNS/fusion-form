prototype(Neos.Fusion.Form:FieldComponent)  < prototype(Neos.Fusion:Component) {

    #
    # populate field context to be available during prop evaluation
    #
    @context.field = ${this.field ? this.field : Form.createField(form, this.name, this.multiple)}

    @propTypes {
        field = ${PropTypes.instanceOf('Neos\Fusion\Form\Domain\Model\Field')}
        name = ${PropTypes.string}
        multiple = ${PropTypes.boolean}
        id = ${PropTypes.string}
        class = ${PropTypes.string}
        required = ${PropTypes.boolean}
        value = ${PropTypes.any}
        attributes = ${PropTypes.any}
    }

    #
    # values that are used to populate the field context
    #
    field = null
    name = null
    multiple = false

    #
    # generic attributes that all fields item components should support
    #
    id = null
    class = null
    required = false
    value = null
    attributes = Neos.Fusion:DataStructure
}

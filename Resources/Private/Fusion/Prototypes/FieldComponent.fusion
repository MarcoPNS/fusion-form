prototype(Neos.Fusion.Form:FieldComponent) < prototype(Neos.Fusion:Component) {

    #
    # ensure the `field`-context is evaluated first so it is available for calculating other `props`
    #
    @context.field = ${this.field}

    #
    # `field` is excluded from `props`
    #
    @ignoreProperties = ${['field']}

    @propTypes {
        field = ${PropTypes.instanceOf('\\Neos\\Fusion\\Form\\Eel\\Field')}
        attributes = ${PropTypes.dataStructure({})}
        content = ${PropTypes.string}
    }

    #
    # the `field` context
    #
    field = Neos.Fusion.Form:FieldDefinition

    #
    # attributes for the field to be used by the renderer of derived types
    #
    attributes = Neos.Fusion:DataStructure

    #
    # afx content to be used by the renderer of derived types
    #
    content = ''
}

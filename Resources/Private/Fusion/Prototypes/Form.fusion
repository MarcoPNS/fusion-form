prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion.Form:FormComponent) {

    @propTypes {
        attributes = ${PropTypes.dataStructure()}
        content = ${PropTypes.string}
    }

    #
    # html attributes for the form
    #
    attributes = Neos.Fusion:DataStructure {
        action = Neos.Fusion:UriBuilder
        method = 'post'
        enctype = ${(String.toLowerCase(this.method) == 'post') ? 'multipart/form-data' : null}
    }

    #
    # form content
    #
    content = null

    renderer = afx`
        <form
            {...props.attributes}
        >
            <div style="display: none">
                <Neos.Fusion:Loop
                    items={Form.calculateHiddenFields(form, props.content)}
                    itemKey="fieldName"
                    itemName="fieldValue"
                >
                    <input type="hidden" name={fieldName} value={fieldValue} />
                </Neos.Fusion:Loop>
                <Neos.Fusion:Join
                    @if.isNoGetRequest={String.toLowerCase(props.attributes.method) != 'get'}
                    @if.isSessionPresent={Security.isAuthenticated()}
                >
                    <input
                        @cache.mode="uncached"
                        @cache.context={[]}
                        type="hidden"
                        name="__csrfToken"
                        value={Security.csrfToken()}
                    />
                </Neos.Fusion:Join>
            </div>
            {props.content}
        </form>
    `
}

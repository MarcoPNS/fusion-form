prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion.Form:FormComponent) {

    @propTypes {
        attributes = ${PropTypes.dataStructure()}
        content = ${PropTypes.string}
    }

    #
    # html attributes for the form
    #
    attributes = Neos.Fusion:DataStructure

    #
    # form content
    #
    content = null

    renderer = afx`
        <form
            action={props.form.target}
            method={props.form.method}
            enctype={props.form.encoding}
            {...props.attributes}
        >
            <div style="display: none">
                <Neos.Fusion:Loop
                    items={Form.calculateHiddenFields(form, props.content)}
                    itemKey="fieldName"
                    itemName="fieldValue"
                >
                    <input type="hidden" name={fieldName} value={fieldValue} />
                </Neos.Fusion:Loop>
                <Neos.Fusion:Join
                    @if.isNoGetRequest={String.toLowerCase(props.form.method) != 'get'}
                    @if.isSessionPresent={Security.isAuthenticated()}
                >
                    <input
                        @cache.mode="uncached"
                        @cache.context={[]}
                        type="hidden"
                        name="__csrfToken"
                        value={Security.csrfToken()}
                    />
                </Neos.Fusion:Join>
            </div>
            {props.content}
        </form>
    `
}

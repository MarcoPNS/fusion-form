prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion.Form:FormComponent) {

    @propTypes {
        attributes = ${PropTypes.dataStructure({})}
        content = ${PropTypes.string}
    }

    #
    # html attributes for the form
    #
    attributes = Neos.Fusion:DataStructure

    #
    # form content
    #
    content = null

    renderer = afx`
        <form
            action={form.getTarget()}
            method={form.getMethod()}
            enctype={form.getEncoding}
            {...props.attributes}
        >
            <div style="display: none">
                <Neos.Fusion:Loop
                    items={form.calculateHiddenFields(props.content)}
                    itemKey="fieldName"
                    itemName="fieldValue"
                >
                    <input type="hidden" name={fieldName} value={fieldValue} />
                </Neos.Fusion:Loop>
                <Neos.Fusion:Join
                    @if.isNoGetRequest={String.toLowerCase(form.getMethod()) != 'get'}
                    @if.isSessionPresent={Security.isAuthenticated()}
                >
                    <input
                        @cache.mode="uncached"
                        @cache.context={[]}
                        type="hidden"
                        name="__csrfToken"
                        value={Security.csrfToken()}
                    />
                </Neos.Fusion:Join>
            </div>
            {props.content}
        </form>
    `
}

prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion.Form:FormComponent) {

    #
    # html attributes for the form
    #
    action = Neos.Fusion:UriBuilder
    method = 'post'
    enctype = ${(String.toLowerCase(this.method) == 'post') ? 'multipart/form-data' : null}

    #
    # form content
    #
    content = null

    renderer = afx`
        <form
            {...Array.set(props, 'content', null)}
        >
            <div style="display: none">
                <Neos.Fusion:Loop
                    items={Form.calculateHiddenFields(form, props.content)}
                    itemKey="fieldName"
                    itemName="fieldValue"
                >
                    <input type="hidden" name={fieldName} value={fieldValue} />
                </Neos.Fusion:Loop>
                <Neos.Fusion:Join
                    @if.isNoGetRequest={String.toLowerCase(props.method) != 'get'}
                    @if.isSessionPresent={Security.isAuthenticated()}
                >
                    <input
                        @cache.mode="uncached"
                        @cache.context={[]}
                        type="hidden"
                        name="__csrfToken"
                        value={Security.csrfToken()}
                    />
                </Neos.Fusion:Join>
            </div>
            {props.content}
        </form>
    `
}

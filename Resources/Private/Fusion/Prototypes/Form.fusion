prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion:Component) {
    @context.form = ${Form.createFormDefinition(this.request, this.fieldnamePrefix, this.data)}

    @propTypes {
        request = ${PropTypes.instanceOf('Neos\Flow\Mvc\ActionRequest')}
        fieldnamePrefix = ${PropTypes.string}
        data = ${PropTypes.any}
        action = ${PropTypes.string}
        method = ${PropTypes.string}
        enctype = ${PropTypes.string}
        id = ${PropTypes.string}
        class = ${PropTypes.string}
        attributes = ${PropTypes.any}
        content = ${PropTypes.string}
    }

    #
    # values that are used to populate the field context
    #
    request = ${request}
    fieldnamePrefix = null
    data = Neos.Fusion:DataStructure

    #
    # html attributes for the form
    #
    id = null
    class = null
    action = Neos.Fusion:UriBuilder
    method = 'post'
    enctype = null
    attributes = Neos.Fusion:DataStructure

    #
    # afx content when `Neos.Fusion.Form:Form`
    #
    content = null

    renderer = afx`
        <form
            {...props.attributes}
            id={props.id}
            class={props.class}
            action={props.action}
            method={props.method}
            enctype={props.enctype}
        >
            <div style="display: none">
                <Neos.Fusion:Loop
                    items={Form.calculateHiddenFields(request, form.fieldNamePrefix, props.data, props.content)}
                    itemKey="fieldName"
                    itemName="fieldValue"
                >
                    <input type="hidden" name={fieldName} value={fieldValue} />
                </Neos.Fusion:Loop>
            </div>

            {props.content}
        </form>
    `
}

// this should go to fusion itself on the long run
prototype(Neos.Fusion.Form:Fragment) < prototype(Neos.Fusion:Component) {
  renderer = ${props.content}
}

prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion:Component) {
    @context.form = ${Form.createFormDefinition(this.request, this.name, this.fieldnamePrefix, this.object)}

    @propTypes {
        request = ${PropTypes.instanceOf('Neos\Flow\Mvc\ActionRequest')}
        name = ${PropTypes.string}
        fieldnamePrefix = ${PropTypes.string}
        object = ${PropTypes.any}
        action = ${PropTypes.string}
        method = ${PropTypes.string}
        enctype = ${PropTypes.string}
        id = ${PropTypes.string}
        class = ${PropTypes.string}
        attributes = ${PropTypes.any}
        content = ${PropTypes.string}
    }

    #
    # values that are used to populate the field context
    #
    request = ${request}
    name = null
    fieldnamePrefix = null
    object = null

    #
    # html attributes for the form
    #
    id = null
    class = null
    action = Neos.Fusion:UriBuilder
    method = 'post'
    enctype = null
    attributes = Neos.Fusion:DataStructure

    #
    # afx content when `Neos.Fusion.Form:Form`
    #
    content = null

    renderer = afx`
        <form
            {...props.attributes}
            id={props.id}
            class={props.class}
            action={props.action}
            method={props.method}
            enctype={props.enctype}
        >
            <div style="display: none">
                <Neos.Fusion.Form:Fragment>
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@package]"} value={request.controllerPackageKey} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@subpackage]"} value={request.controllerSubpackageKey} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@controller]"} value={request.controllerName} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@action]"} value={request.controllerActionName} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][arguments]"} value={Form.argumentsWithHmac(request.arguments)} />
                </Neos.Fusion.Form:Fragment>

                <Neos.Fusion.Form:Fragment @if.isNotMainRequest={request != request.mainRequest}>
                    <input type="hidden" name="__referrer[@package]" value={request.parentRequest.controllerPackageKey} />
                    <input type="hidden" name="__referrer[@subpackage]" value={request.parentRequest.controllerSubpackageKey} />
                    <input type="hidden" name="__referrer[@controller]" value={request.parentRequest.controllerName} />
                    <input type="hidden" name="__referrer[@action]" value={request.parentRequest.controllerActionName} />
                    <input type="hidden" name="__referrer[arguments]" value={Form.argumentsWithHmac(request.parentRequest.arguments, request.argumentNamespace)} />
                </Neos.Fusion.Form:Fragment>

                <Neos.Fusion:Loop items={Form.emptyHiddenFieldnames(props.content, form.fieldNamePrefix)} itemName="fieldname">
                    <input type="hidden" name={fieldname} value="" />
                </Neos.Fusion:Loop>

                <input
                    type="hidden"
                    name={Form.prefixFieldName('__trustedProperties', form.fieldNamePrefix)}
                    value={Form.trustedPropertiesToken(props.content, form.fieldNamePrefix)}
                    />

                <input type="hidden" name="__csrfToken" value={Form.csrfToken()} />
            </div>



            {props.content}
        </form>
    `
}

// this should go to fusion itself on the long run
prototype(Neos.Fusion.Form:Fragment) < prototype(Neos.Fusion:Component) {
  renderer = ${props.content}
}

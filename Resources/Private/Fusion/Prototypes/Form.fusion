prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion:Component) {

    @context.form = ${this.form}

    @propTypes {
        fieldnamePrefix = ${PropTypes.string}
        data = ${PropTypes.any}
        actionUri = ${PropTypes.string}
        method = ${PropTypes.string}
        enctype = ${PropTypes.string}
        id = ${PropTypes.string}
        class = ${PropTypes.string}
        name = ${PropTypes.string}
        attributes = ${PropTypes.any}
        content = ${PropTypes.string}
    }

    #
    # values that are used to populate the field context
    #
    fieldnamePrefix = null
    data = Neos.Fusion:DataStructure
    form = ${Form.createForm(request, this.fieldnamePrefix, this.data)}

    #
    # html attributes for the form
    #
    id = null
    class = null
    name = null
    actionUri = Neos.Fusion:UriBuilder
    method = 'post'
    enctype = ${(String.toLowerCase(this.method) == 'post') ? 'multipart/form-data' : null}
    attributes = Neos.Fusion:DataStructure

    #
    # form content
    #
    content = null

    renderer = afx`
        <form
            id={props.id}
            class={props.class}
            name={props.name}
            action={props.actionUri}
            method={props.method}
            enctype={props.enctype}
            {...props.attributes}
        >
            <div style="display: none">
                <Neos.Fusion:Loop
                    items={Form.calculateHiddenFields(form, props.content)}
                    itemKey="fieldName"
                    itemName="fieldValue"
                >
                    <input type="hidden" name={fieldName} value={fieldValue} />
                </Neos.Fusion:Loop>
                <Neos.Fusion:Join
                    @if.isNoGetRequest={String.toLowerCase(props.method) != 'get'}
                    @if.isSessionPresent={Security.isAuthenticated()}
                >
                    <input
                        @cache.mode="uncached"
                        @cache.context={[]}
                        type="hidden"
                        name="__csrfToken"
                        value={Security.csrfToken()}
                    />
                </Neos.Fusion:Join>
            </div>
            {props.content}
        </form>
    `
}

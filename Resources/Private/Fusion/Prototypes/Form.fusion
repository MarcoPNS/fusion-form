prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion:Component) {

    #
    # populate form context to be available during prop evaluation
    #
    @context.form = ${Form.createForm(request, this.fieldnamePrefix, this.data)}

    @propTypes {
        fieldnamePrefix = ${PropTypes.string}
        data = ${PropTypes.any}
        actionUri = ${PropTypes.string}
        method = ${PropTypes.string}
        enctype = ${PropTypes.string}
        id = ${PropTypes.string}
        class = ${PropTypes.string}
        name = ${PropTypes.string}
        attributes = ${PropTypes.any}
        content = ${PropTypes.string}
    }

    #
    # values that are used to populate the field context
    #
    fieldnamePrefix = null
    data = Neos.Fusion:DataStructure

    #
    # html attributes for the form
    #
    id = null
    class = null
    name = null
    actionUri = Neos.Fusion:UriBuilder
    method = 'post'
    enctype = 'multipart/form-data'
    attributes = Neos.Fusion:DataStructure

    #
    # form content
    #
    content = null

    renderer = afx`
        <form
            id={props.id}
            class={props.class}
            name={props.name}
            action={props.actionUri}
            method={props.method}
            enctype={props.enctype}
            {...props.attributes}
        >
            <div style="display: none">
                <Neos.Fusion:Loop
                    items={Form.calculateHiddenFields(form, props.content)}
                    itemKey="fieldName"
                    itemName="fieldValue"
                >
                    <input type="hidden" name={fieldName} value={fieldValue} />
                </Neos.Fusion:Loop>
                <Neos.Fusion:Join
                    @if.isNoGetRequest={props.method != 'get'}
                >
                    <input
                        @cache.mode="uncached"
                        @cache.context={[]}
                        type="hidden"
                        name="__csrfToken"
                        value={Security.csrfToken()}
                    />
                </Neos.Fusion:Join>
            </div>
            {props.content}
        </form>
    `
}

prototype(Neos.Fusion.Form:Neos.BackendModule.FieldContainer)  < prototype(Neos.Fusion.Form:FieldComponent) {

    @propTypes {
        label = ${PropTypes.string}
        content = ${PropTypes.string}
    }

    #
    # label and afx content
    #
    label = null
    content = null

    #
    # translations source and package for labels and errors
    #
    translation =  Neos.Fusion:DataStructure {
        label = Neos.Fusion:DataStructure {
            package = 'Neos.Neos'
            source = 'Modules'
        }
        error = Neos.Fusion:DataStructure {
            package = 'Neos.Flow'
            source = 'ValidationErrors'
        }
    }

    renderer = afx`
        <div class={field.errors ? "neos-control-group neos-error" : "neos-control-group"}>
            <label class="neos-control-label" for={field.name} @if.has={props.label}>
                {I18n.translate(props.label, props.label, [], props.translation.label.source, props.translation.label.package)}
            </label>
            <div class="neos-controls">
                {props.content}
            </div>
            <ul @if.hasErrors={field.errors} class="errors">
                <Neos.Fusion:Loop items={field.result.flattenedErrors} itemName="errors" >
                    <Neos.Fusion:Loop items={errors} itemName="error" >
                        <li>
                            <span class="neos-help-block">
                                {I18n.translate(error.code, error, [], props.translation.error.source, props.translation.error.package)}
                            </span>
                        </li>
                    </Neos.Fusion:Loop>
                </Neos.Fusion:Loop>
            </ul>
        </div>
    `

    #
    # FieldComponent that are rendered inside field container are adjusted
    # if no specific name is given the field from the container is reused
    # if nop specific id is given the name from the comntainer is used
    #
    prototype(Neos.Fusion.Form:FieldComponent) {
        field = ${this.name ? Form.createField(form, this.name, this.multiple) : field}
        id = ${field.name}
    }
}

